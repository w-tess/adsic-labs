*************************************************************
* 01AUXPE Advanced DEsign for Signal Integrity and Compliance
* Stefano Grivet-Talocia, DET Politecnico di Torino, Italy
* This file generated by SGT on 15-Feb-2023
*************************************************************

*************************************************************
* Signal Integrity testcase IBM_topology_2
*************************************************************
* Three coupled differential lines connecting two chips
* through a board, from C4 bumps to C4 bumps and routed
* through modules (including module vias), LGA socket,
* board (including board via and board wiring)
* Reference system: IBM z16
* This example courtesy of Xiaomin Duan and Hubert Harrer
* IBM Deutschland R&D GmbH, Boeblingen, Germany
*************************************************************


*************************************************************
* SPICE models of all interconnects (generated by IdEM12)
*************************************************************
* All interconnects have 12 ports. Connection scheme is
* depicted below for the three differential pairs.
* ------------------
* 1 --- 2
* 3 --- 4
* ------------------
* 5 --- 6
* 7 --- 8
* ------------------
* 9 --- 10
* 11 --- 12
* ------------------
*************************************************************
* Board via field (three version with different stub lengths)
.include subckt_1_BoardVia_backdrill.cir
.include subckt_2_BoardVia_noStub.cir
.include subckt_3_BoardVia_withStub.cir
* C4 escape wires (1mm length, two different materials)
.include subckt_4_C4escape_1mm_highloss_85ohm.cir
.include subckt_5_C4escape_1mm_lowloss_85ohm.cir
* LGA socket
.include subckt_6_LGA.cir
* Module vias
.include subckt_7_Module_via.cir
* Wiring on module (two lengths and two materials)
.include subckt_8_Module_wire_1mm_highloss_85ohm.cir
.include subckt_9_Module_wire_1mm_lowloss_85ohm.cir
.include subckt_10_Module_wire_5mm_highloss_85ohm.cir
.include subckt_11_Module_wire_5mm_lowloss_85ohm.cir
* Wiring on PCB (two lenghts and two materials)
.include subckt_12_PCB_wire_0p25inch_highloss_85ohm.cir
.include subckt_13_PCB_wire_0p25inch_lowloss_85ohm.cir
.include subckt_14_PCB_wire_2p0inch_highloss_85ohm.cir
.include subckt_15_PCB_wire_2p0inch_lowloss_85ohm.cir
*************************************************************


*************************************************************
* Instantiate interconnect models - IBM Topology 2
* Node numbering: Y_kz with
* Y: section along channel
* k: index of (single-ended) interconnect
* z: {p,n} denoting positive/negative nodes of each port
*    [negative nodes {n} of all ports are grounded]
*************************************************************

* A-B: C4 escape wire: near end (comment/uncomment to select model)
X_C4_AB
+ A_1p 0 B_1p 0
+ A_2p 0 B_2p 0
+ A_3p 0 B_3p 0
+ A_4p 0 B_4p 0
+ A_5p 0 B_5p 0
+ A_6p 0 B_6p 0
*+ subckt_4_C4escape_1mm_highloss_85ohm
 + subckt_5_C4escape_1mm_lowloss_85ohm

* B-C: Module wiring (comment/uncomment to select model)
X_Module_BC
+ B_1p 0 C_1p 0
+ B_2p 0 C_2p 0
+ B_3p 0 C_3p 0
+ B_4p 0 C_4p 0
+ B_5p 0 C_5p 0
+ B_6p 0 C_6p 0
* + subckt_8_Module_wire_1mm_highloss_85ohm
 + subckt_9_Module_wire_1mm_lowloss_85ohm
*+ subckt_10_Module_wire_5mm_highloss_85ohm
* + subckt_11_Module_wire_5mm_lowloss_85ohm

* C-D: Module vias
X_Module_Vias_CD
+ C_1p 0 D_1p 0
+ C_2p 0 D_2p 0
+ C_3p 0 D_3p 0
+ C_4p 0 D_4p 0
+ C_5p 0 D_5p 0
+ C_6p 0 D_6p 0
+ subckt_7_Module_via

* D-E: LGA socket
X_LGA_Socket_DE
+ D_1p 0 E_1p 0
+ D_2p 0 E_2p 0
+ D_3p 0 E_3p 0
+ D_4p 0 E_4p 0
+ D_5p 0 E_5p 0
+ D_6p 0 E_6p 0
+ subckt_6_LGA

* E-F: Board vias
X_Board_Vias_EF
+ E_1p 0 F_1p 0
+ E_2p 0 F_2p 0
+ E_3p 0 F_3p 0
+ E_4p 0 F_4p 0
+ E_5p 0 F_5p 0
+ E_6p 0 F_6p 0
+ subckt_1_BoardVia_backdrill
* + subckt_2_BoardVia_noStub
* + subckt_3_BoardVia_withStub

* F-G: Board wires
X_Board_Wires_FG
+ F_1p 0 G_1p 0
+ F_2p 0 G_2p 0
+ F_3p 0 G_3p 0
+ F_4p 0 G_4p 0
+ F_5p 0 G_5p 0
+ F_6p 0 G_6p 0
* + subckt_12_PCB_wire_0p25inch_highloss_85ohm
 + subckt_13_PCB_wire_0p25inch_lowloss_85ohm
*+ subckt_14_PCB_wire_2p0inch_highloss_85ohm
* + subckt_15_PCB_wire_2p0inch_lowloss_85ohm

* G-H: Board vias (NE and FE reversed wrt E-F)
X_Board_Vias_GH
+ H_1p 0 G_1p 0
+ H_2p 0 G_2p 0
+ H_3p 0 G_3p 0
+ H_4p 0 G_4p 0
+ H_5p 0 G_5p 0
+ H_6p 0 G_6p 0
+ subckt_1_BoardVia_backdrill
* + subckt_2_BoardVia_noStub
* + subckt_3_BoardVia_withStub

* H-I: LGA socket (NE and FE reversed wrt D-E)
X_LGA_Socket_HI
+ I_1p 0 H_1p 0
+ I_2p 0 H_2p 0
+ I_3p 0 H_3p 0
+ I_4p 0 H_4p 0
+ I_5p 0 H_5p 0
+ I_6p 0 H_6p 0
+ subckt_6_LGA

* I-L: Module vias (NE and FE reversed wrt C-D)
X_Module_Vias_IL
+ L_1p 0 I_1p 0
+ L_2p 0 I_2p 0
+ L_3p 0 I_3p 0
+ L_4p 0 I_4p 0
+ L_5p 0 I_5p 0
+ L_6p 0 I_6p 0
+ subckt_7_Module_via

* L-M: Module wiring
X_Module_LM
+ L_1p 0 M_1p 0
+ L_2p 0 M_2p 0
+ L_3p 0 M_3p 0
+ L_4p 0 M_4p 0
+ L_5p 0 M_5p 0
+ L_6p 0 M_6p 0
* + subckt_8_Module_wire_1mm_highloss_85ohm
 + subckt_9_Module_wire_1mm_lowloss_85ohm
*+ subckt_10_Module_wire_5mm_highloss_85ohm
* + subckt_11_Module_wire_5mm_lowloss_85ohm

* M-N: C4 escape wire: far end (comment/uncomment to select model)
X_C4_MN
+ M_1p 0 N_1p 0
+ M_2p 0 N_2p 0
+ M_3p 0 N_3p 0
+ M_4p 0 N_4p 0
+ M_5p 0 N_5p 0
+ M_6p 0 N_6p 0
*+ subckt_4_C4escape_1mm_highloss_85ohm
 + subckt_5_C4escape_1mm_lowloss_85ohm


*************************************************************
* Terminations, drivers, receivers
*************************************************************

* Termination, differential line 1, near end
X_NE_term1
+ A_1p 0
+ A_2p 0
+ diffTermination4nodes

* Termination, differential line 1, far end
X_FE_term1
+ N_1p 0
+ N_2p 0
+ diffTermination4nodes


* Driver, differential line 2, near end
X_NE_term2
+ DRIVER_OUT 0
+ NULL 0
+ diffDriver4nodes


* Termination, differential line 2, far end
X_FE_term2
+ N_3p 0
+ N_4p 0
+ diffTermination4nodes


* Termination, differential line 3, near end
X_NE_term3
+ A_5p 0
+ A_6p 0
+ diffTermination4nodes

* Termination, differential line 3, far end
X_FE_term3
+ N_5p 0
+ N_6p 0
+ diffTermination4nodes


.param Rs=10 Cd=0.1p Tbit=0.1e-9 Vp=1 Trise={Tbit*0.2}
* Driver (pure differential, no grounding here)
.subckt diffDriver4nodes a_1 b_1 a_2 b_2
R_s1 a_1 a_10 {Rs}
R_s2 a_2 a_20 {Rs}
E_s1 a_10 b_1 drv 0 0.5
E_s2 a_20 b_2 drv 0 -0.5
*Vdrv drv 0 DC 0 AC 1 pwl(0 0 10p 1 100n 1)
*Vdrv drv 0 DC 0 AC 1 pwl(0 0 10p 1 40p 1 50p 0 100n 0)
*Vdrv drv 0 DC 0 AC 1 pwl(0 0 20p 1 100p 1 120p 0 100n 0)
Vdrv drv 0 DC 0 AC 1 pwl(0 0 {Trise} 1 {Tbit} 1 {Tbit+Trise} 0 100n 0)
*xPRBS drv 0 VPRBS params: TB={TBit} Vmax={Vp} TR={Trise} seed=0
Rdum drv 0 1
.ends

* Termination (pure differental, no grounding here)
.subckt diffTermination4nodes a_1 b_1 a_2 b_2
R_d a_1 a_2 85
C_d a_1 a_2 {Cd}
.ends




*BFFEB0 N001 0 V=V(DRIVER_OUT)*-7.623918197980212e-02
*A3 N001 0 CLK 0 0 0 M0 0 SAMPLEHOLD

xFFEB0 DRIVER_OUT CLK 0 M0 INT0 FFE_BLOCK params: TB={TBit} DELAY=1p COEFFICIENT=-7.623918197980212e-02
xFFEB1 INT0       CLK 0 M1 INT1 FFE_BLOCK params: TB={TBit} DELAY=1 COEFFICIENT=-6.436958726140170e-02
xFFEB2 INT1       CLK 0 M2 INT2 FFE_BLOCK params: TB={TBit} DELAY=2 COEFFICIENT=8.593346296756370e-01
xFFEB3 INT2       CLK 0 M3 INT3 FFE_BLOCK params: TB={TBit} DELAY=3 COEFFICIENT=5.657064203054559e-05
xFFEB4 INT3       CLK 0 M4 INT4 FFE_BLOCK params: TB={TBit} DELAY=4 COEFFICIENT=3.043736493321492e-08
xFFEB5 INT4       CLK 0 M5 INT5 FFE_BLOCK params: TB={TBit} DELAY=5 COEFFICIENT=3.762388212350572e-12
xFFEB6 INT5       CLK 0 M6 INT6 FFE_BLOCK params: TB={TBit} DELAY=6 COEFFICIENT=1.188044856886521e-15

Bsum A_3p_IDEAL 0 V=V(M0)+V(M1)+V(M2)+V(M3)+V(M4)+V(M5)+V(M6)
R_s1 A_3p A_3p_IDEAL 10
Bsum_minus A_4p_IDEAL 0 V=-(V(M0)+V(M1)+V(M2)+V(M3)+V(M4)+V(M5)+V(M6))
R_s2 A_4p A_4p_IDEAL 10


* Clock generator to synchronize fir firter
Vclk CLK 0 PULSE(0 1 {TBit/2} 1p 1p {TBit/2} {TBit})


* Node1=V_in; Node2=V_clk; Node3=gnd; Node4=V_out
.subckt FFE_BLOCK 1 2 3 4 5 params: TB=1 DELAY=1 COEFFICIENT=1
R1 1 3 1e9
B1 4 3 V=V(N001)*{COEFFICIENT}
B2 5 3 V=delay(V(N001), 5p)
A3 1 3 2 0 0 0 N001 0 SAMPLEHOLD
.ends




* auxiliary subcircuit to generate a PRBS signal (rise time is zero!)
* NRZ voltage swing: Vmax; Bit time: TB; Rise/Fall time: TR
* Use (positive integer) seed to shift PRBS sequence forward (randomize)
.subckt VPRBS 1 2 params: TB=1 Vmax=5 TR=0.1 seed=0
.param T=2*TB F=1/T BR=1/TB
* This generates a PRBS with zero rise times
B1 a 0 V={Vmax}*(rand(time/{TB}+{seed})>=.5)
R1 a 0 1
* This generates a trapezoidal wave with desired rise/fall times
* (it is a convolution with a pulse with {TR} width, exploiting the piecewise constant nature of signal B1)
B2 1 2 V= (time/{TR}-{TB}*floor(time/{TB})/{TR})*V(a)  + ({TB}*floor(time/{TB})/{TR} - time/{TR} + 1) * absdelay(V(a),{TR})
Ro 1 2 1
.ends
